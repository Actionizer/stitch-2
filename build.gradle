plugins {
	id "java"
	id "java-library"
	id "maven-publish"
	id "dev.yumi.gradle.licenser" version "1.2.+"
}

def ENV = System.getenv()
version = "0.6.3" + (ENV.GITHUB_ACTIONS ? "" : "+local")

group = 'net.fabricmc'
base.archivesName = project.name.toLowerCase()

repositories {
	mavenCentral()
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
}

configurations {
	ship
	enigma
	implementation.extendsFrom ship
	compileOnly.extendsFrom enigma
	testImplementation.extendsFrom enigma
}

dependencies {
	ship 'org.ow2.asm:asm:9.1'
	ship 'org.ow2.asm:asm-commons:9.1'
	ship 'org.ow2.asm:asm-tree:9.1'
	ship 'org.ow2.asm:asm-util:9.1'
	ship 'net.fabricmc:tiny-mappings-parser:0.3.0+build.17'
	implementation 'com.google.guava:guava:32.0.0-jre'
	compileOnly 'org.jetbrains:annotations:20.1.0'

	enigma 'cuchaz:enigma:0.23.2'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

license {
	rule file("HEADER")

	include "**/*.java"
	exclude "**/*.properties"
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Stitch',
				'Implementation-Version': archiveVersion,
				'Main-Class': "net.fabricmc.stitch.Main"
	}
}

tasks.register('allJar', Jar) {
	from {
		configurations.ship.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	manifest {
		attributes 'Implementation-Title': 'Stitch',
				'Implementation-Version': archiveVersion,
				'Main-Class': "net.fabricmc.stitch.Main"
	}
	archiveClassifier = 'all'
	with jar
}

java {
	withSourcesJar()
	sourceCompatibility = 8
	targetCompatibility = 8
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 8
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact(allJar)
		}
	}

	// select the repositories you want to publish to
	repositories {
		if (ENV.MAVEN_URL) {
			repositories.maven {
				name "fabric"
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}

test {
	useJUnitPlatform()
}

// A task to ensure that the version being released has not already been released.
tasks.register('checkVersion') {
	doFirst {
		def xml = new URL("https://maven.muonmc.org/net/fabricmc/stitch/maven-metadata.xml").text
		def metadata = new XmlSlurper().parseText(xml)
		def versions = metadata.versioning.versions.version*.text();
		if (versions.contains(version)) {
			throw new RuntimeException("${version} has already been released!")
		}
	}
}

publish.mustRunAfter checkVersion
